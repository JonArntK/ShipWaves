#pragma kernel ComputeWaterSurfaceKernel

#include "ComputeElevationGlobal.hlsl"
#include "GenerateMesh.hlsl"

#define GROUP_SIZE_X 64
#define GROUP_SIZE_Y 1
#define GROUP_SIZE_Z 1


// Define buffers for storing vertices, normals and texture coordiantes of the mesh.
RWByteAddressBuffer _VertexBuffer;
RWByteAddressBuffer _NormalBuffer;
RWByteAddressBuffer _TexcoordBuffer;

// Other definitions.
int _QuadCount, _XQuadCount, _ZQuadCount, _XOrigin, _ZOrigin;
float _Time, _XStep, _ZStep;

StructuredBuffer<float3> _VesselCoord;
int _NumVessels;
int2 _VesselNxNy;

StructuredBuffer<float4> _VesselPath;
int _VesselPathNumPoints;
 

float ComputeElevation(Vertex v)
{
    float elevation = 0;
    for (int i = 0; i < _NumVessels; i++)
    {
        elevation += ComputeShipWaveElevationGlobal(v.position.x, v.position.z, i, _VesselCoord, _VesselNxNy, _VesselPath, _VesselPathNumPoints);
    }
    return elevation;
}

// Generate vertex for grid of quads, from 1D array index (branchless version).
Vertex GenerateQuad (uint id) 
{
    float instance = floor(float(id + 0.00001) / 6.0); // Index of current quad.
    float3 center = float3(floor(Mod(instance, _XQuadCount)), 0.0, floor(instance / _XQuadCount)); // Center of current quad.

    Vertex vertex;
    float u = Mod(float(id), 2.0);                          // Local x-position of quad.
    float v = sign(Mod(126.0, Mod(float(id), 6.0) + 6.0));  // Local z-position of quad.
    float3 localPos = float3(u, 0, v);

    vertex.position = (localPos + center) * float3(_XStep, 1, _ZStep) + float3(_XOrigin, 0, _ZOrigin);  // Assigning position to vertex.
    vertex.texcoord = float2(u, v);
    return vertex;
}


 
[numthreads(GROUP_SIZE_X, GROUP_SIZE_Y, GROUP_SIZE_Z)]
void ComputeWaterSurfaceKernel(uint3 threadID : SV_DispatchThreadID)
{
    uint id = threadID.x + threadID.y * GROUP_SIZE_X;
    if ((int)id >= _QuadCount) return;
    uint idx1 = id * 6;
    uint idx2 = id * 6 + 1;
    uint idx3 = id * 6 + 2;  
    uint idx4 = id * 6 + 3;  
    uint idx5 = id * 6 + 4;  
    uint idx6 = id * 6 + 5;  

    // Generate quad.
    Vertex v1 = GenerateQuad(idx1);
    Vertex v2 = GenerateQuad(idx2);
    Vertex v3 = GenerateQuad(idx3); 
    Vertex v6 = GenerateQuad(idx6); 

    // Compute surface elevation.
    v1.position.y = ComputeElevation(v1);
    v2.position.y = ComputeElevation(v2);
    v3.position.y = ComputeElevation(v3);
    v6.position.y = ComputeElevation(v6);

    // ----- Triangluar mesh from quads -----
    // Quads are generated in the order displayed below
    //    p3, p5        p6
    //       ___________
    //      |\          |
    //      |  \        |
    //      |    \      |
    //      |      \    |
    //      |        \  |
    //      |__________\|
    //     p1           p2, p4
    //
    // Hence triangles are made as (p1, p2, p3) and (p4, p5, p6),
    // which is equivalent to      (p1, p2, p3) and (p2, p3, p6),
    // avoiding the computation of p4 and p5.

    StoreTriangle(idx1, idx2, idx3, v1, v3, v2, _VertexBuffer, _NormalBuffer, _TexcoordBuffer);
    StoreTriangle(idx4, idx5, idx6, v2, v3, v6, _VertexBuffer, _NormalBuffer, _TexcoordBuffer);
}
